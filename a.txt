Function GetCSVData(filePath As String) As Variant
    Dim fso As Object, ts As Object
    Dim lines() As String, parts() As String
    Dim i As Long, j As Long
    Dim maxCols As Long
    Dim dataArr() As Variant

    Set fso = CreateObject("Scripting.FileSystemObject")
    Set ts = fso.OpenTextFile(filePath, 1)

    lines = Split(ts.ReadAll, vbCrLf)
    ts.Close

    ' First pass to get the max number of columns
    maxCols = 0
    For i = 0 To UBound(lines)
        If Trim(lines(i)) <> "" Then
            parts = Split(lines(i), ",")
            If UBound(parts) + 1 > maxCols Then
                maxCols = UBound(parts) + 1
            End If
        End If
    Next i

    ReDim dataArr(1 To UBound(lines) + 1, 1 To maxCols)

    ' Second pass to fill array
    For i = 0 To UBound(lines)
        If Trim(lines(i)) <> "" Then
            parts = Split(lines(i), ",")
            For j = 0 To UBound(parts)
                dataArr(i + 1, j + 1) = Trim(parts(j))
            Next j
        End If
    Next i

    GetCSVData = dataArr
End Function
